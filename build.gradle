plugins {
    id "com.palantir.docker" version "0.25.0"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "java"
    id "org.springframework.boot" version "2.3.4.RELEASE"
    id "ro.isdc.wro4j.gradle" version "1.8.0.Beta5" apply false
}

apply plugin: "wro4j"

ext {
    bootstrapVersion = "3.3.6"
    jqueryUiVersion = "1.11.4"
    jqueryVersion = "2.2.4"
    jacocoVersion = "0.8.2"
    wavefrontVersion = "2.0.0-RC1"
    springCloudVersion = "Hoxton.SR4"
}

webResources {
    srcMainDir = project.file("src/main")
    srcTestDir = project.file("src/test")

    bundle("petclinic"){
        css "webjars/bootstrap/${bootstrapVersion}/less/bootstrap.less"
        css "less/petclinic.less"
        dstStaticFolder "static/resources/css"
        preProcessor "lessCssImport"
        postProcessor "less4j"
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.spring.io/snapshot")
    }

    maven {
        url = uri("https://repo.spring.io/milestone")
    }

    maven {
        url = uri("https://repo.maven.apache.org/maven2")
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator:2.3.0.RC1"
    implementation "org.springframework.boot:spring-boot-starter-validation:2.3.0.RC1"
    implementation "org.springframework.boot:spring-boot-starter-cache:2.3.0.RC1"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc:2.3.0.RC1"
    implementation "org.springframework.boot:spring-boot-starter-web:2.3.0.RC1"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf:2.3.0.RC1"
    implementation "org.flywaydb:flyway-core:6.4.1"
    implementation "com.github.ben-manes.caffeine:caffeine:2.8.2"
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth:2.2.2.RELEASE"
    implementation "com.wavefront:wavefront-spring-boot-starter:2.0.0-RC1"
    implementation "org.webjars:webjars-locator-core:0.45"
    implementation "org.webjars:jquery:${jqueryVersion}"
    implementation "org.webjars:jquery-ui:${jqueryUiVersion}"
    implementation "org.webjars:bootstrap:${bootstrapVersion}"
    implementation "org.springframework.boot:spring-boot-devtools:2.3.0.RC1"
    runtimeOnly "mysql:mysql-connector-java:8.0.20"
    testImplementation "org.springframework.boot:spring-boot-starter-test:2.3.0.RC1"
    testImplementation "org.testcontainers:mysql:1.14.1"
    webjars "org.webjars:bootstrap:${bootstrapVersion}"
}

group = "org.springframework.samples"
version = "2.1.0.BUILD-SNAPSHOT"
description = "petclinic"
sourceCompatibility = "1.8"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

docker {
    name "${project.hasProperty('id') ? project.id : 'kakaopay'}/${project.name}:${project.version}"
    files bootJar.archiveFile.get()
    dockerfile project.file("./docker/Dockerfile")
    buildArgs(["TZ": "Asia/Seoul", "JAR_FILE": "${jar.archiveFileName.get()}"])
}

task dockerLogin(type: Exec) {
    if (project.hasProperty('id') && project.hasProperty('pw')) {
        commandLine 'docker', 'login', "-u", "${project.id}", "-p", "${project.pw}"
    }
}

docker.dependsOn build
dockerPush.dependsOn dockerLogin
